// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
      /* select whether to increment or emit out(t) */
      Inc16(in=pcout, out=addout); 
      Mux16(a=pcout, b=addout, sel=inc, out=incout);

      /* select whether to increment or load *
      /* if `load`, then `inc` does not apply */
      Mux16(a=incout, b=in, sel=load, out=loadout);

      /* select whether to load or reset */
      Mux16(a=loadout, b[0..15]=false, sel=reset, out=rout);

      /* access the register */
      Register(in=rout, load=true, out=pcout, out=out);
}
